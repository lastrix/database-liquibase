buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath "org.liquibase:liquibase-gradle-plugin:2.2.1"
        classpath "org.liquibase:liquibase-core:4.25.1"
        classpath "org.postgresql:postgresql:42.7.1"
    }
}

plugins {
    id "org.liquibase.gradle" version "2.2.1"
    id 'idea'
    id 'java'
    id 'groovy'
}

repositories {
    mavenLocal()
    mavenCentral()
}

group 'mps-liquibase'
version '1.0'
sourceCompatibility = '17'

ext {
    version_liquibase = "4.25.1"
    version_liquibase_gradle_plugin = "2.2.1"
    version_liquibase_groovy_dsl = "3.0.3"
    version_postgresql = "42.7.1"
    version_jaxb = "2.3.1"
    version_logback = "1.4.14"
    version_snakeyaml = "2.2"
    version_picocli = "4.7.5"
}


dependencies {
    liquibaseRuntime "info.picocli:picocli:${version_picocli}"
    liquibaseRuntime "org.liquibase:liquibase-core:${version_liquibase}"
    liquibaseRuntime "org.liquibase:liquibase-groovy-dsl:${version_liquibase_groovy_dsl}"
    liquibaseRuntime "org.postgresql:postgresql:${version_postgresql}"
    liquibaseRuntime "javax.xml.bind:jaxb-api:${version_jaxb}"
    liquibaseRuntime "ch.qos.logback:logback-classic:${version_logback}"
    liquibaseRuntime "org.yaml:snakeyaml:${version_snakeyaml}"
}

ext {
    // liquibase variables
    liquibase_login = System.getProperty('db.login') ?: 'mps_user'
    liquibase_password = System.getProperty('db.password') ?: "mps_password"
    liquibase_server = System.getProperty('db.server') ?: 'localhost'
    liquibase_dbname = System.getProperty('db.name') ?: 'mps'
    liquibase_port = System.getProperty('db.port') ?: '15432'
    liquibase_schema = System.getProperty('db.schema') ?: 'payment'
    liquibase_change_log = System.getProperty('db.change.log.file') ?: 'changelog.xml'
    liquibase_context = System.getProperty('db.context') ?: 'database,schema'
    liquibase_run_list = System.getProperty('db.run.list') ?: 'main'
    liquibase_log_level = System.getProperty('db.log.level') ?: 'info'
}

liquibase {
    activities {
        main {
            url "jdbc:postgresql://${liquibase_server}:${liquibase_port}/${liquibase_dbname}?currentSchema=${liquibase_schema}"
            username liquibase_login
            password liquibase_password
            changelogFile liquibase_change_log
            contexts "${liquibase_context}"
            logLevel liquibase_log_level
            changeLogParameters([ DB_SCHEMA: liquibase_schema])
            defaultSchemaName liquibase_schema
        }
    }
    runList = liquibase_run_list
}
